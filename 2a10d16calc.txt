G2:=QuasisimpleMatrixGroup("2A10",16,0:Automorphisms:=true);
_,G2i:=IsomorphicCopy(G2);
ChiefFactors(G2i);
/*
    G
    |  Cyclic(2)
    *
    |  Alternating(10)
    *
    |  Cyclic(2)
    1
*/


G := sub<Generic(G2)|G2.1,G2.2>;
ChiefFactors(G);
/*
    G
    |  Alternating(10)
    *
    |  Cyclic(2)
    1
*/
M := GModule(G);
a := ActionGenerator(M,1); b := ActionGenerator(M,2);
Order(a), Order(b), IsScalar((a*b)^8), IsScalar((a*b*b)^12);
// 3, 9, true, true
// Thus these are the standard generators.

//check no bad primes in the generators
LCM({ Denominator(x) : x in Eltseq(a) }); //1
LCM({ Denominator(x) : x in Eltseq(b) }); //1
LCM({ Denominator(x) : x in Eltseq(G2.3) }); //3
// Thus we will need to consider the behaviour of the automorphism when p=3 separately.

D := GModule(G, [Transpose(a^-1), Transpose(b^-1)] ); //dual module
h := GHom(M,D);
form := Matrix(Rationals(), h.1); //the form preserved

// check no bad primes in the form
LCM({ Denominator(x) : x in Eltseq(form) }); // 6
form := 6*form;
d:= Determinant(form);
Factorisation(IntegerRing()!d);
// [ <5,8> ]
// Thus, no bad primes

// L induces the nontrivial outer automorphism of 2.A_10
L:=G2.3;
assert IsScalar(L*form*Transpose(L)*form^-1);
(L*form*Transpose(L)*form^-1)[1][1];
// 5/9

// Hence we need to rescale L by 3/Root(5) to preserve the form.
// Thus we need to work in the number field generated by x^2-5

Z<x>:=PolynomialRing(IntegerRing());
F<r5> := NumberField(x^2-5);

M:=MatrixAlgebra(F,16);

Gr := sub<GL(16,F) | M!G2.1, M!G2.2, M!(G2.3)*(3/r5)>;

form:=M!form;

for i in [1..3] do;
	assert Gr.i*form*Transpose(Gr.i) eq form;
end for;
// Hence all entries of Gr now preserve the form.

Lr := Gr.3;
assert Determinant(Lr) eq 1;

// We now compute the spinor norm of Lr

A := Identity(M) - M!Lr;
N := Nullspace(A);
Dimension(N); //8
E := ExtendBasis(N,Generic(N));

e:=[];
for i in [Dimension(N)+1..16] do;
	e := e cat Eltseq(E[i]);
end for;

B := Matrix(F,8,16,e);

tt := B*A*form*Transpose(B);
IsSquare(Determinant(tt)); //true 1

// Hence when r5 exists over the finite field, the matrix inducing the outer automorphism has spinor norm 1.


// We perform a separate calculation for the case p=3.

Gp3 := QuasisimpleMatrixGroup("2A10",16,3:Automorphisms:=true);
isit,formp3,_,scal:=SymmetricBilinearForm(Gp3:Scalars:=true);
assert isit;
scal; //[1,1,2]

IsSquare(scal[3]); // false

// Hence the outer automorphism here cannot be rescaled to preserve the form



