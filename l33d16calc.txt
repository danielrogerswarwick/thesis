//L3(3) in dimension 16, characteristics not 3 or 13

G := QuasisimpleMatrixGroup("L33",16,0);
_,Gis:=IsomorphicCopy(G);
ChiefFactors(Gis);
/*
    G
    |  A(2, 3)			= L(3,3)
    1
*/
// Hence G is PSL(3,3)

M:=GModule(G);
K<w>:=BaseRing(M);
MinimalPolynomial(w); // $.1^4 + $.1^3 + 2*$.1^2 - 4*$.1 + 3

A:=ActionGenerator(M,1); B:=ActionGenerator(M,2);
// Check standard generators
Order(A),Order(B),Order(A*B),Order(A*B*A*B^2);
// 2, 3, 13, 4, so standard generators

// Check no bad primes in the matrices
LCM({ Denominator(x) : x in Eltseq(A) }); //1
LCM({ Denominator(x) : x in Eltseq(B) }); //27
// No bad primes


DM := GModule(G, [Transpose(A^-1),Transpose(B^-1)] ); //dual of M

// Construct images of the outer automorphism
imA:=A;
imB:=B * A * B * A^-1 * B^-1 * A * B * A * B^-1 * A^-1 * B^-1 * A * B * A * B * A^-1
* B^-1 * A * B^-1 * A;

// Check these images give standard generators
Order(imA),Order(imB),Order(imA*imB),Order(imA*imB*imA*imB*imB);
// 2,3,13,4

AM := GModule(G, [imA,imB] ); //2 aut


dh := GHom(DM,AM); 
assert Dimension(dh) eq 1; // Confirms that AM and DM are isomorphic
dd:=Determinant(dh.1);
IsSquare(dd/-3); //true;

// Find the form preserved in the unitary case

Roots(PolynomialRing(K)!MinimalPolynomial(w)); // Gives image of the conjugation map

/*
[
    <w, 1>,
    <1/3*(-w^3 - 3*w^2 - 5*w), 1>,
    <1/3*(-w^3 - 2*w + 3), 1>,
    <1/3*(2*w^3 + 3*w^2 + 4*w - 6), 1>
]
*/

conj:=hom<K->K | 1/3*(-w^3 - 2*w + 3)>;
assert conj(conj(w)) eq w; // so conj acts as conjugation
star := function(m)
  return Transpose(MatrixAlgebra(K,16)![ conj(e): e in Eltseq(m) ]);
end function;
Aa := star(A); Ba := star(B);
DMS := GModule(G,[Aa^-1, Ba^-1]);
hh:=GHom(M,DMS);
form:=hh.1;

//check no bad primes in the matrix hh.1 of the form
LCM({ Denominator(x) : x in Eltseq(form) }); //4
LCM({ Denominator(x) : x in Eltseq(form^-1) }); //27
// So only bad prime is 2
// When q is even, there are no diagonal automorphisms, so class is stabilised by gamma.


